import "@cadl-lang/rest";
import "@cadl-lang/versioning";
import "@cadl-lang/compiler";
import "@azure-tools/cadl-providerhub";
import "@azure-tools/cadl-azure-core";
import "@azure-tools/cadl-autorest";
import "@azure-tools/cadl-azure-resource-manager";

import "./webmodels/common/agentDetails.cadl";
import "./webmodels/common/siteSpnProperties.cadl";
import "./webmodels/common/hypervSiteUsage.cadl";
import "./webmodels/common/common.cadl";
import "./webmodels/common/jobProperties.cadl";
import "./webmodels/common/highAvailability.cadl";
import "./webmodels/common/siteHealthSummaryCollection.cadl";
import "./webmodels/common/siteHealthErrorDetails.cadl";
import "./webmodels/common/siteErrorSummaryCollection.cadl";
import "./webmodels/common/siteUsageResponse.cadl";
import "./webmodels/common/machineMetadata.cadl";
import "./webmodels/common/runAsAccountProperties.cadl";
import "./webmodels/common/OperationStatusProperties.cadl";

import "./webmodels/vmware/vmwareSiteProperties.cadl";
import "./webmodels/vmware/vmwareMachineProperties.cadl";
import "./webmodels/vmware/vmwareVcenterProperties.cadl";
import "./webmodels/vmware/vmwareHostProperties.cadl";

import "./webmodels/hyperv/hyperVSiteProperties.cadl";
import "./webmodels/hyperv/hypervClusterProperties.cadl";
import "./webmodels/hyperv/hyperv.cadl";
import "./webmodels/hyperv/hypervHostProperties.cadl";
import "./webmodels/hyperv/hypervMachineProperties.cadl";
import "./webmodels/hyperv/machineMetaDataProperties.cadl";

import "./webmodels/server/serverSiteProperties.cadl";
import "./webmodels/server/serverSiteUsageResponse.cadl";
import "./webmodels/server/serverProperties.cadl";
import "./webmodels/server/serverOperationStatusProperties.cadl";

import "./webmodels/import/importSiteProperties.cadl";
import "./webmodels/import/importJobProperties.cadl";
import "./webmodels/import/importMachineProperties.cadl";
import "./webmodels/import/importCommon.cadl";

using Autorest;
using Cadl.Http;
using Cadl.Rest;
using Cadl.Versioning;
using Azure.Core;
using Azure.ResourceManager;
using OpenAPI;

@armProviderNamespace
@service({
  title: "SingleSPNAttribute",
})
@versionedDependency(
  [
    [
      Versions.v2018_05_01_preview,
      Azure.ResourceManager.Versions.v1_0_Preview_1
    ],
    [
      Versions.v2019_05_01_preview,
      Azure.ResourceManager.Versions.v1_0_Preview_1
    ],
    [Versions.v2020_01_01, Azure.ResourceManager.Versions.v1_0_Preview_1],
    [
      Versions.v2020_01_01_preview,
      Azure.ResourceManager.Versions.v1_0_Preview_1
    ],
    [Versions.v2020_02_01, Azure.ResourceManager.Versions.v1_0_Preview_1],
    [Versions.v2020_07_07, Azure.ResourceManager.Versions.v1_0_Preview_1],
    [Versions.v2020_07_10, Azure.ResourceManager.Versions.v1_0_Preview_1],
    [
      Versions.v2020_08_01_preview,
      Azure.ResourceManager.Versions.v1_0_Preview_1
    ]
  ]
)
@versioned(Versions)
@doc("Azure OffAzure Resource Provider management API.")
namespace Microsoft.OffAzure;

interface Operations extends Azure.ResourceManager.Operations {}

enum Versions {
  v2018_05_01_preview: "2018-05-01-preview",
  v2019_05_01_preview: "2019-05-01-preview",
  v2020_01_01: "2020-01-01",
  v2020_01_01_preview: "2020-01-01-preview",
  v2020_02_01: "2020-02-01",
  v2020_07_07: "2020-07-07",
  v2020_07_10: "2020-07-10",
  v2020_08_01_preview: "2020-08-01-preview",
}

#suppress "@azure-tools/cadl-azure-resource-manager/no-resource-delete-operation"
#suppress "@azure-tools/cadl-azure-core/casing-style"
@armResourceOperations
interface HyperVClusterInt {
  @tag("HyperVClusterController")
  @doc("Method to get a Hyper-V cluster.")
  @added(Versions.v2018_05_01_preview)
  @operationId("HyperVCluster_GetCluster")
  get is ArmResourceRead<HyperVCluster>;

  @tag("HyperVClusterController")
  @added(Versions.v2018_05_01_preview)
  @operationId("HyperVCluster_PutCluster")
  create is ArmResourceCreateOrUpdateAsync<HyperVCluster>;

  // @tag("HyperVClusterController")
  // delete is ArmResourceDeleteSync<HyperVCluster>;

  @added(Versions.v2018_05_01_preview)
  @tag("HyperVClusterController")
  @segment("clusters")
  @operationId("HyperVCluster_GetAllClustersInSite")
  listByParent is ArmResourceListByParent<
    HyperVCluster,
    {
      @doc("filter query")
      @query
      $filter: string;
      ...SubscriptionIdParameter;
      ...ResourceGroupParameter;
      ...ApiVersionParameter;
    }
  >;
}

#suppress "@azure-tools/cadl-azure-core/casing-style"
#suppress "@azure-tools/cadl-azure-resource-manager/arm-resource-path-segment-invalid-chars"
@doc("A VMwareSite")
model VMwareSite is TrackedResource<SitesProperties> {
  @key("siteName")
  @segment("VMwareSites")
  @doc("Site name")
  @path
  @pattern("^[a-zA-Z0-9-]{3,24}$")
  name: string;
}

@armResourceOperations
interface VmwareSites {
  @tag("SitesController")
  @added(Versions.v2018_05_01_preview)
  @operationId("Sites_GetSite")
  get is ArmResourceRead<VMwareSite>;

  @added(Versions.v2018_05_01_preview)
  @operationId("Sites_PutSite")
  create is ArmResourceCreateOrUpdateAsync<VMwareSite>;

  @added(Versions.v2018_05_01_preview)
  @operationId("Sites_PatchSite")
  update is ArmResourcePatchSync<VMwareSite, SitesProperties>;

  @tag("SitesController")
  @added(Versions.v2018_05_01_preview)
  @operationId("Sites_DeleteSite")
  delete is ArmResourceDeleteSync<VMwareSite>;

  @tag("SitesController")
  @doc("Method to generate report containing \r\n            machine and the deep discovery of the application installed in the machine.")
  @added(Versions.v2018_05_01_preview)
  ExportApplications is ArmResourceActionAsync<VMwareSite, {}, {}>;

  @doc("Method to refresh a site.")
  @tag("SitesController")
  @added(Versions.v2018_05_01_preview)
  @operationId("Sites_RefreshSite")
  refresh is ArmResourceActionAsync<VMwareSite, {}, {}>;

  @doc("Method to get site health summary.")
  @armResourceRead(InnerError)
  @segment("healthSummary")
  @added(Versions.v2018_05_01_preview)
  @get
  healthSummary(
    ...ResourceInstanceParameters<VMwareSite>
  ): ArmResponse<SiteHealthSummaryCollection>;

  @doc("Method to get site error summary.")
  @armResourceRead(InnerError)
  @segment("errorSummary")
  @added(Versions.v2018_05_01_preview)
  @get
  errorSummary(
    ...ResourceInstanceParameters<VMwareSite>,

    @doc("VmwareSite.")
    @path
    @pattern("^[a-zA-Z0-9-]{3,24}$")
    vmwareSite: string
  ): ArmResponse<SiteErrorSummaryCollection>;

  @doc("Method to get site error summary.")
  @armResourceRead(InnerError)
  @segment("usage")
  @added(Versions.v2018_05_01_preview)
  @get
  usage(
    ...ResourceInstanceParameters<VMwareSite>,

    @doc("VmwareSite.")
    @path
    @pattern("^[a-zA-Z0-9-]{3,24}$")
    vmwareSite: string
  ): ArmResponse<SiteUsageResponse>;

  @added(Versions.v2019_05_01_preview)
  @tag("VMwarePropertiesController")
  @doc("Method to get site error summary.")
  UpdateDependencyMapStatus is ArmResourceActionAsync<VMwareSite, {}, {}>;

  @added(Versions.v2019_05_01_preview)
  @tag("VMwarePropertiesController")
  @doc("Method to associate Run as account to machine\r\n            in a site.")
  UpdateRunAsAccount is ArmResourceActionAsync<VMwareSite, {}, {}>;

  @added(Versions.v2019_05_01_preview)
  @tag("VMwarePropertiesController")
  @doc("Method to associate Run as account to machine\r\n            in a site.")
  UpdateTags is ArmResourceActionAsync<VMwareSite, {}, {}>;

  //TODO {subscriptionId}" delete
  @added(Versions.v2019_05_01_preview)
  @doc("Method to update properties for machines   in a site.")
  @tag("VMwarePropertiesController")
  UpdateProperties is ArmResourceActionNoContentAsync<
    VMwareSite,
    MachineMetadataList
  >;

  @doc("Method to get site usage.")
  @added(Versions.v2020_01_01)
  @operationId("Sites_GetSiteUsage")
  summary is ArmResourceActionAsync<
    VMwareSite,
    {},
    ResponseWithHypervSiteUsageSchema<200>
  >;

  @doc("Method to generate report containing \r\n            machine and the deep discovery of the application installed in the machine.")
  @tag("SitesController")
  @added(Versions.v2020_01_01)
  ExportMachines is ArmResourceActionSync<
    VMwareSite,
    {},
    ResponseWithVmwareSiteUsageSchema<200>
  >;

  @doc("Method to generate report containing \r\n            machine and the errors encountered during guest discovery of the machine.")
  @tag("SitesController")
  @added(Versions.v2020_01_01)
  ExportMachineErrors is ArmResourceActionAsync<
    VMwareSite,
    {},
    ResponseWithVmwareSiteUsageSchema<200>
  >;

  @doc("API to generate coarse map for the list of machines.")
  @tag("DependencyMapController")
  @added(Versions.v2020_01_01_preview)
  generateCoarseMap is ArmResourceActionAsync<
    VMwareSite,
    {},
    ResponseWithVmwareSiteUsageSchema<200>
  >;

  @doc("API to generate detailed map for a selected machine.")
  @tag("DependencyMapController")
  @added(Versions.v2020_01_01_preview)
  generateDetailedMap is ArmResourceActionAsync<
    VMwareSite,
    {},
    ResponseWithVmwareSiteUsageSchema<200>
  >;

  @doc("API to list client group members for the selected client group.")
  @tag("DependencyMapController")
  @added(Versions.v2020_01_01_preview)
  clientGroupsMembers is ArmResourceActionAsync<
    VMwareSite,
    {},
    ResponseWithVmwareSiteUsageSchema<200>
  >;

  @doc("API to list server group members for the selected server group.")
  @tag("DependencyMapController")
  @added(Versions.v2020_01_01_preview)
  serverGroupMembers is ArmResourceActionAsync<
    VMwareSite,
    {},
    ResponseWithVmwareSiteUsageSchema<200>
  >;

  @doc("API to generate report containing agentless dependencies.")
  @tag("DependencyMapController")
  @added(Versions.v2020_01_01_preview)
  exportDependencies is ArmResourceActionAsync<
    VMwareSite,
    {},
    ResponseWithVmwareSiteUsageSchema<200>
  >;
}

@doc("An machine resource belonging to a site resource.")
@parentResource(VMwareSite)
model MachineResource is ProxyResource<VmwareMachineProperties> {
  @doc("Machine name")
  @key("machineName")
  @segment("machines")
  @path
  @pattern("^[a-zA-Z0-9-]{3,24}$")
  name: string;
}

@armResourceOperations
interface Machines {
  @tag("MachinesController")
  @added(Versions.v2018_05_01_preview)
  @operationId("Machines_GetMachine")
  get is ArmResourceRead<MachineResource>;

  @tag("MachinesController")
  @added(Versions.v2018_05_01_preview)
  @operationId("Machines_GetAllMachinesInSite")
  listByParent is ArmResourceListByParent<
    MachineResource,
    {
      ...SubscriptionIdParameter;
      ...ResourceGroupParameter;
      ...ApiVersionParameter;

      #suppress "@azure-tools/cadl-azure-core/casing-style"
      @doc("filter query")
      @query
      $filter: string;

      #suppress "@azure-tools/cadl-azure-core/casing-style"
      @doc("page size  query")
      @query
      $top: string;

      @query
      @doc("Optional parameter for continuation token.")
      continuationToken: string;

      @query
      @doc("Total count of machines in the given site.")
      totalRecordCount: int32;
    }
  >;
  //
  @tag("MachinesController")
  @doc("Method to stop a machine.")
  @added(Versions.v2018_05_01_preview)
  @operationId("Machines_StopMachine")
  stop is ArmResourceActionAsync<MachineResource, {}, {}>;

  @tag("MachinesController")
  @doc("Method to start a machine.")
  @added(Versions.v2018_05_01_preview)
  @operationId("Machines_StartMachine")
  start is ArmResourceActionAsync<MachineResource, {}, {}>;

  @doc("Method to get machine with application and roles present in it.")
  @segment("applications")
  @armResourceRead(InnerError)
  @tag("VmwareMachinesController")
  @added(Versions.v2018_05_01_preview)
  @get
  applications(
    ...ResourceInstanceParameters<MachineResource>
  ): ArmResponse<MachineResource>;

  #suppress "@azure-tools/cadl-azure-resource-manager/arm-resource-operation-missing-api-version"
  @added(Versions.v2020_07_10)
  @armResourceRead(InnerError)
  @summary("Method to get a machines software inventory like applications and roles.")
  @doc("Method to get a machines software inventory like applications and roles.")
  @route("/subscriptions/{subscriptionId}/resourceGroups/{resourceGroupName}/providers/Microsoft.OffAzure/VMwareSites/{siteName}/machines/{machineName}/softwareInventory/default")
  @get
  @operationId("Machines_GetMachineSoftwareInventory")
  Seven10MachinesControllerGetMachineSoftwareInventory is Azure.Core.Foundations.Operation<
    {
      @doc("The ID of the target subscription.")
      @path
      subscriptionId: string;

      @doc("The name of the resource group. The name is case insensitive.")
      @path
      resourceGroupName: string;

      @doc("Site name.")
      @path
      siteName: string;

      @doc("Machine ARM name.")
      @path
      machineName: string;

      // @doc("Accept header")
      // @header
      // accept: "application/json, text/json, application/xml, text/xml";
    },
    VmwareMachineSoftwareInventory
  >;
}

#suppress "@azure-tools/cadl-azure-core/casing-style"
@doc("A vcenter resource belonging to a site resource.")
@parentResource(VMwareSite)
model VCenter is ProxyResource<VCenterProperties> {
  @doc(" VCenters name")
  @key("vcenterName")
  @segment("vCenters")
  @path
  @pattern("^[a-zA-Z0-9-]{3,24}$")
  name: string;
}

@tag("VCenterController")
@armResourceOperations
@added(Versions.v2018_05_01_preview)
interface VmwareVcenters {
  @operationId("VCenter_GetVCenter")
  get is ArmResourceRead<VCenter>;
  @operationId("VCenter_PutVCenter")
  create is ArmResourceCreateOrUpdateAsync<VCenter>;
  @operationId("VCenter_DeleteVCenter")
  delete is ArmResourceDeleteSync<VCenter>;
  @operationId("VCenter_GetAllVCentersInSite")
  listByParent is ArmResourceListByParent<VCenter>;
}

@doc("A runasaccount resource belonging to a site resource.")
@parentResource(VMwareSite)
model VmwareRunAsAccountResource is ProxyResource<RunAsAccountProperties> {
  @doc(" RunAsAccounts name")
  @key("accountName")
  @segment("runAsAccounts")
  @path
  @pattern("^[a-zA-Z0-9-]{3,24}$")
  name: string;
}

@armResourceOperations
@tag("RunAsAccountsController")
@added(Versions.v2018_05_01_preview)
interface VmwareRunAsAccounts {
  @operationId("RunAsAccounts_GetRunAsAccount")
  get is ArmResourceRead<VmwareRunAsAccountResource>;
  @operationId("RunAsAccounts_GetAllRunAsAccountsInSite")
  listByParent is ArmResourceListByParent<VmwareRunAsAccountResource>;
}

@doc("A host resource belonging to a site resource.")
@parentResource(VMwareSite)
model VmwareHostResource is ProxyResource<VmwareHostProperties> {
  @doc(" Hosts name")
  @key("hostName")
  @segment("hosts")
  @path
  @pattern("^[a-zA-Z0-9-]{3,24}$")
  name: string;
}

@tag("Microsoft.Azure.FDS.WebRole.V2020_01_01_preview.VMwareHostController")
@armResourceOperations
interface VmwareHosts {
  @added(Versions.v2020_01_01_preview)
  get is ArmResourceRead<VmwareHostResource>;

  @added(Versions.v2020_01_01_preview)
  listByParent is ArmResourceListByParent<VmwareHostResource>;
}

@doc("A host resource belonging to a site resource.")
@parentResource(VMwareSite)
model VmwareJobResource is ProxyResource<JobProperties> {
  @doc(" Jobs name")
  @key("jobName")
  @segment("jobs")
  @path
  @pattern("^[a-zA-Z0-9-]{3,24}$")
  name: string;
}

@armResourceOperations
@added(Versions.v2018_05_01_preview)
@tag("JobsController")
interface VmwareJobs {
  @operationId("Jobs_GetJob")
  get is ArmResourceRead<VmwareJobResource>;
  @operationId("Jobs_GetAllJobsInSite")
  listByParent is ArmResourceListByParent<VmwareJobResource>;
}

#suppress "@azure-tools/cadl-azure-core/casing-style"
#suppress "@azure-tools/cadl-azure-resource-manager/arm-resource-path-segment-invalid-chars"
@doc("A HyperV SiteResource")
model HyperVSite is TrackedResource<SiteProperties> {
  @key("siteName")
  @segment("HyperVSites")
  @doc("Site name")
  @path
  @pattern("^[a-zA-Z0-9-]{3,24}$")
  name: string;
}

@armResourceOperations
interface HypervSites {
  @tag("HyperVSitesController")
  @added(Versions.v2018_05_01_preview)
  @operationId("HyperVSites_GetSite")
  get is ArmResourceRead<HyperVSite>;

  @tag("HyperVSitesController")
  @operationId("HyperVSites_PutSite")
  @added(Versions.v2018_05_01_preview)
  create is ArmResourceCreateOrUpdateAsync<HyperVSite>;

  @tag("HyperVSitesController")
  @added(Versions.v2018_05_01_preview)
  @operationId("HyperVSites_PatchSite")
  update is ArmResourcePatchSync<HyperVSite, SitesProperties>;

  @tag("HyperVSitesController")
  @added(Versions.v2018_05_01_preview)
  @operationId("HyperVSites_DeleteSite")
  delete is ArmResourceDeleteSync<HyperVSite>;

  @doc("Method to get site health summary.")
  @armResourceRead(InnerError)
  @segment("healthSummary")
  @added(Versions.v2018_05_01_preview)
  @get
  healthSummary(
    ...ResourceInstanceParameters<HyperVSite>
  ): ArmResponse<SiteHealthSummaryCollection>;

  @doc("Method to get site error summary.")
  @armResourceRead(InnerError)
  @added(Versions.v2018_05_01_preview)
  @segment("errorSummary")
  @get
  errorSummary(
    ...ResourceInstanceParameters<HyperVSite>,

    @doc("Appliance Name")
    @query
    applianceName: string
  ): ArmResponse<SiteErrorSummaryCollection>;

  @doc("Method to get site error summary.")
  @armResourceRead(InnerError)
  @segment("usage")
  @added(Versions.v2018_05_01_preview)
  @get
  usage(
    ...ResourceInstanceParameters<HyperVSite>
  ): ArmResponse<SiteUsageResponse>;

  @tag("HyperVSitesController")
  @doc("Method to refresh a site.")
  @added(Versions.v2018_05_01_preview)
  @operationId("HyperVSites_RefreshSite")
  refresh is ArmResourceActionAsync<HyperVSite, {}, {}>;

  @doc("Method to get site usage.")
  @added(Versions.v2020_01_01)
  @operationId("HyperVSites_GetSiteUsage")
  summary is ArmResourceActionAsync<
    HyperVSite,
    {},
    ResponseWithHypervSiteUsageSchema<200>
  >;

  @tag("Microsoft.Azure.FDS.WebRole.V2020_08_01_preview.HyperVDependencyMapController")
  @doc("API to generate coarse map for the list of machines.")
  @added(Versions.v2020_08_01_preview)
  generateCoarseMap is ArmResourceActionAsync<
    HyperVSite,
    {},
    ResponseWithHypervSiteUsageSchema<200>
  >;

  @tag("Microsoft.Azure.FDS.WebRole.V2020_08_01_preview.HyperVDependencyMapController")
  @doc("API to generate detailed map for a selected machine.")
  @added(Versions.v2020_08_01_preview)
  generateDetailedMap is ArmResourceActionAsync<
    HyperVSite,
    {},
    ResponseWithHypervSiteUsageSchema<200>
  >;

  @tag("Microsoft.Azure.FDS.WebRole.V2020_08_01_preview.HyperVDependencyMapController")
  @doc("API to list client group members for the selected client group.")
  @added(Versions.v2020_08_01_preview)
  clientGroupMembers is ArmResourceActionAsync<
    HyperVSite,
    {},
    ResponseWithHypervSiteUsageSchema<200>
  >;

  @tag("Microsoft.Azure.FDS.WebRole.V2020_08_01_preview.HyperVDependencyMapController")
  @doc("API to list server group members for the selected server group.")
  @added(Versions.v2020_08_01_preview)
  serverGroupMembers is ArmResourceActionAsync<
    HyperVSite,
    {},
    ResponseWithHypervSiteUsageSchema<200>
  >;

  @tag("Microsoft.Azure.FDS.WebRole.V2020_08_01_preview.HyperVDependencyMapController")
  @doc("API to generate report containing agentless dependencies.")
  @added(Versions.v2020_08_01_preview)
  exportDependencies is ArmResourceActionAsync<
    HyperVSite,
    {},
    ResponseWithHypervSiteUsageSchema<200>
  >;

  @tag("Microsoft.Azure.FDS.WebRole.V2020_08_01_preview.HyperVDependencyMapController")
  @doc("Method to enable disable dependency map status for machines\r\n            in a site.")
  @added(Versions.v2020_08_01_preview)
  UpdateDependencyMapStatus is ArmResourceActionAsync<
    HyperVSite,
    {},
    ResponseWithHypervSiteUsageSchema<200>
  >;

  @tag("Microsoft.Azure.FDS.WebRole.V2020_08_01_preview.HyperVDependencyMapController")
  @doc("Method to generate report containing\r\n            machine and the deep discovery of the application installed in the machine.")
  @added(Versions.v2020_08_01_preview)
  ExportApplications is ArmResourceActionAsync<
    HyperVSite,
    {},
    ResponseWithHypervSiteUsageSchema<200>
  >;

  @tag("Microsoft.Azure.FDS.WebRole.V2020_08_01_preview.HyperVDependencyMapController")
  @doc("Method to generate report containing \r\n            machine and the errors encountered during guest discovery of the machine.")
  @added(Versions.v2020_08_01_preview)
  ExportMachineErrors is ArmResourceActionAsync<
    HyperVSite,
    {},
    ResponseWithHypervSiteUsageSchema<200>
  >;
}

@doc("A job resource belonging to a site resource.")
@parentResource(HyperVSite)
model HypervJobResource is ProxyResource<JobProperties> {
  @doc(" Job name")
  @key("jobName")
  @segment("jobs")
  @path
  @pattern("^[a-zA-Z0-9-]{3,24}$")
  name: string;
}

@doc("A cluster resource belonging to a site resource.")
@parentResource(HyperVSite)
model HypervOperationsStatusResource is ProxyResource<HyperVClusterProperties> {
  @doc(" Cluster name")
  @key("clusterName")
  @segment("clusters")
  @path
  @pattern("^[a-zA-Z0-9-]{3,24}$")
  name: string;
}

#suppress "@azure-tools/cadl-azure-core/casing-style"
@doc("A cluster resource belonging to a site resource.")
@parentResource(HyperVSite)
model HyperVCluster is ProxyResource<HyperVClusterProperties> {
  @doc(" Cluster ARM name")
  @key("clusterName")
  @segment("clusters")
  @path
  @pattern("^[a-zA-Z0-9-]{3,24}$")
  name: string;
}

@tag("HyperVJobController")
@added(Versions.v2018_05_01_preview)
@armResourceOperations
interface HypervJobs {
  @operationId("HyperVJobs_GetJob")
  get is ArmResourceRead<HypervJobResource>;
  @operationId("HyperVJobs_GetAllJobsInSite")
  listByParent is ArmResourceListByParent<HypervJobResource>;
}

#suppress "@azure-tools/cadl-azure-core/casing-style"
@doc("A host resource belonging to a site resource.")
@parentResource(HyperVSite)
model HyperVHost is ProxyResource<HyperVHostProperties> {
  @doc(" Host name")
  @key("hostName")
  @segment("hosts")
  @path
  @pattern("^[a-zA-Z0-9-]{3,24}$")
  name: string;
}

#suppress "@azure-tools/cadl-azure-core/casing-style"
@armResourceOperations
interface HyperVHostInt {
  @tag("HyperVHostController")
  @added(Versions.v2018_05_01_preview)
  @operationId("HyperVHost_GetHost")
  get is ArmResourceRead<HyperVHost>;

  @tag("Microsoft.Azure.FDS.WebRole.V2020_01_01_preview.HyperVHostController")
  @added(Versions.v2018_05_01_preview)
  @operationId("HyperVHost_PutHost")
  create is ArmResourceCreateOrUpdateAsync<HyperVHost>;

  @tag("HyperVHostController")
  delete is ArmResourceDeleteSync<HyperVHost>;

  @tag("HyperVHostController")
  @segment("hosts")
  @added(Versions.v2018_05_01_preview)
  @operationId("HyperVHost_GetAllHostsInSite")
  listByParent is ArmResourceListByParent<
    HyperVHost,
    {
      @doc("filter query")
      @query
      $filter: string;
      ...SubscriptionIdParameter;
      ...ResourceGroupParameter;
      ...ApiVersionParameter;
    }
  >;
}

@doc("A machine resource belonging to a site resource.")
@parentResource(HyperVSite)
model HypervMachineResource is ProxyResource<HypervMachineProperties> {
  @doc(" HypervMachine name")
  @key("machineName")
  @segment("machines")
  @path
  @pattern("^[a-zA-Z0-9-]{3,24}$")
  name: string;
}

@tag("Microsoft.Azure.FDS.WebRole.V2022_10_27.HyperVMachinesController")
@armResourceOperations
interface HypervMachines {
  @added(Versions.v2018_05_01_preview)
  @operationId("HyperVMachines_GetMachine")
  get is ArmResourceRead<HypervMachineResource>;

  @added(Versions.v2018_05_01_preview)
  @operationId("HyperVMachines_GetAllMachinesInSite")
  listByParent is ArmResourceListByParent<
    HypervMachineResource,
    {
      ...SubscriptionIdParameter;
      ...ResourceGroupParameter;
      ...ApiVersionParameter;

      #suppress "@azure-tools/cadl-azure-core/casing-style"
      @doc("filter query")
      @query
      $filter: string;

      #suppress "@azure-tools/cadl-azure-core/casing-style"
      @doc("page size  query")
      @query
      $top: string;

      @query
      @doc("Optional parameter for continuation token.")
      continuationToken: string;

      @query
      @doc("Total count of machines in the given site.")
      totalRecordCount: int32;
    }
  >;

  @added(Versions.v2018_05_01_preview)
  @doc("Method to update custom properties for HYPERV machines\r\n            in a site.")
  @tag("HyperVMachinesController")
  UpdateProperties is ArmResourceActionNoContentAsync<
    HypervMachineResource,
    MachineMetadataList
  >;
}

@doc("A operation status resource belonging to a site resource.")
@parentResource(VMwareSite)
model VmwareOperationStatusResource
  is ProxyResource<OperationStatusProperties> {
  @doc(" VmwareOperationStatus name")
  @key("operationStatusName")
  @segment("operationsStatus")
  @path
  @pattern("^[a-zA-Z0-9-]{3,24}$")
  name: string;
}

@tag("VmwareOperationsStatusController")
@armResourceOperations
@added(Versions.v2018_05_01_preview)
interface VmwareOperationStatus {
  @operationId("VMwareOperationsStatus_GetOperationStatus")
  get is ArmResourceRead<VmwareOperationStatusResource>;
}


@doc("A operation status resource belonging to a site resource.")
@parentResource(HyperVSite)
model HypervOperationStatusResource
  is ProxyResource<OperationStatusProperties> {
  @doc(" OperationStatus name")
  @key("operationStatusName")
  @segment("operationsStatus")
  @path
  @pattern("^[a-zA-Z0-9-]{3,24}$")
  name: string;
}

@tag("HypervOperationsStatusController")
@armResourceOperations
@added(Versions.v2018_05_01_preview)
interface HypervOperationStatus {
  @operationId("HyperVOperationsStatus_GetOperationStatus")
  get is ArmResourceRead<HypervOperationStatusResource>;
}

@doc("A machine resource belonging to a site resource.")
@parentResource(HyperVSite)
model HypervRunAsAccountResource is ProxyResource<RunAsAccountProperties> {
  @doc(" RunAsAccounts name")
  @key("accountName")
  @segment("runAsAccounts")
  @path
  @pattern("^[a-zA-Z0-9-]{3,24}$")
  name: string;
}

@added(Versions.v2018_05_01_preview)
@armResourceOperations
@tag("HyperVRunAsAccountsController")
interface HypervRunAsAccounts {
  @operationId("HyperVRunAsAccounts_GetRunAsAccount")
  get is ArmResourceRead<HypervRunAsAccountResource>;

  @operationId("HyperVRunAsAccounts_GetAllRunAsAccountsInSite")
  listByParent is ArmResourceListByParent<HypervRunAsAccountResource>;
}

model List<T> {
  @doc("List of elements")
  value: T[];

  @doc("A link to the next page of results if present.")
  nextLink?: url;
}

model Patch<T> {
  @header contentType: "application/merge-patch+json";
  ...T;
}

@doc("A ServerSiteResource")
model ServerSiteResource is TrackedResource<SitesProperties> {
  @key("siteName")
  @segment("serverSites")
  @doc("Site name")
  @path
  @pattern("^[a-zA-Z0-9-]{3,24}$")
  name: string;
}

//*** Server sites tracked resource ***/
@armResourceOperations
@added(Versions.v2019_05_01_preview)
interface ServerSitesInterface {
  get is ArmResourceRead<ServerSiteResource>;
  create is ArmResourceCreateOrUpdateAsync<ServerSiteResource>;
  update is ArmResourcePatchSync<ServerSiteResource, SitesProperties>;
  delete is ArmResourceDeleteSync<ServerSiteResource>;

  @route("/subscriptions/{subscriptionId}/serverSites/{siteName}/refreshSite") //TODO add ServerSites in path
  @doc("Operation to refresh a site")
  RefreshSite is ArmResourceActionNoContentAsync<ServerSiteResource, {}>;

  @route("/subscriptions/{subscriptionId}/serverSites/{siteName}/updateProperties") //TODO add ServerSites in path
  @doc("Operation to update custom properties for servers")
  UpdateProperties is ArmResourceActionNoContentAsync<
    ServerSiteResource,
    MachineMetadataList
  >;

  // TODO: These APIs do not conform to ARM modeling and hence using the raw form
  // of defining them. We will need to fix this in the future.
  @get
  @doc("Get a serve site usage.")
  @segment("usage")
  @armResourceRead(InnerError)
  usage(
    ...ResourceInstanceParameters<ServerSiteResource>
  ): ArmResponse<ServerSiteUsageResponse>;

  @get
  @doc("Get the health summary for a server site.")
  @segment("healthsummary")
  @armResourceRead(InnerError)
  healthSummary(
    ...ResourceInstanceParameters<ServerSiteResource>
  ): ArmResponse<SiteHealthSummaryCollection>;
  //
  @get
  @doc("Get the error summary for a server site.")
  @segment("errorSummary")
  @armResourceRead(InnerError)
  errorSummary(
    ...ResourceInstanceParameters<ServerSiteResource>
  ): ArmResponse<SiteErrorSummaryCollection>;
  //
  @get
  @doc("Get all machines in a site.")
  @segment("machines")
  @armResourceRead(InnerError)
  machines(
    ...ResourceInstanceParameters<ServerSiteResource>
  ): ArmResponse<ServerPropertiesCollection>;
}

//*** Server Machine Proxy Resource ***/

@doc("A machine resource belonging to a site resource.")
@parentResource(ServerSiteResource)
model ServerResource is ProxyResource<ServerProperties> {
  @doc(" A server machine name")
  @key("machineName")
  @segment("machines")
  @path
  @pattern("^[a-zA-Z0-9-]{3,24}$")
  name: string;
}

@armResourceOperations
@added(Versions.v2019_05_01_preview)
interface ServerMachines {
  get is ArmResourceRead<ServerResource>;
  create is ArmResourceCreateOrUpdateSync<ServerResource>;
  delete is ArmResourceDeleteSync<ServerResource>;
}

//*** Server RunAsAccount Proxy Resource ***/

@doc("A run as account resource belonging to a site resource.")
@parentResource(ServerSiteResource)
model ServerRunAsAccountResource is ProxyResource<RunAsAccountProperties> {
  @doc(" RunAsAccounts name")
  @key("accountName")
  @segment("runAsAccounts")
  @path
  @pattern("^[a-zA-Z0-9-]{3,24}$")
  name: string;
}

@armResourceOperations
@added(Versions.v2019_05_01_preview)
interface ServerRunAsAccounts {
  get is ArmResourceRead<ServerRunAsAccountResource>;
  listByParent is ArmResourceListByParent<ServerRunAsAccountResource>;
}

//*** Server OperationsStatus "Proxy" Resource ***/

@doc("A operation status resource belonging to a site resource.")
@parentResource(ServerSiteResource)
model ServerOperationStatusResource
  is ProxyResource<OperationStatusProperties> {
  @doc(" ServerOperationStatus name")
  @key("serverOperationStatusName")
  @segment("operationsStatus")
  @path
  @pattern("^[a-zA-Z0-9-]{3,24}$")
  name: string;
}

@armResourceOperations
@added(Versions.v2019_05_01_preview)
interface ServerOperationStatus {
  get is ArmResourceRead<ServerOperationStatusResource>;
}

//*** Server Jobs Proxy Resource ***/

@doc("A host resource belonging to a site resource.")
@parentResource(ServerSiteResource)
model ServerJobResource is ProxyResource<JobProperties> {
  @doc(" Jobs name")
  @key("jobName")
  @segment("jobs")
  @path
  @pattern("^[a-zA-Z0-9-]{3,24}$")
  name: string;
}

@armResourceOperations
@added(Versions.v2019_05_01_preview)
interface ServerJobs {
  get is ArmResourceRead<ServerJobResource>;
  listByParent is ArmResourceListByParent<ServerJobResource>;
}

@doc("A ImportSiteResource")
model ImportSiteResource is TrackedResource<SitesProperties> {
  @key("siteName")
  @segment("importSites")
  @doc("Site name")
  @path
  @pattern("^[a-zA-Z0-9-]{3,24}$")
  name: string;
}

@doc("A host resource belonging to a site resource.")
@parentResource(ImportSiteResource)
model ImportJobResource is ProxyResource<ImportJobProperties> {
  @doc(" Jobs name")
  @key("jobName")
  @segment("jobs")
  @path
  @pattern("^[a-zA-Z0-9-]{3,24}$")
  name: string;
}
@armResourceOperations
@added(Versions.v2019_05_01_preview)
@tag("ImportJobsController")
interface ImportJobs {
  get is ArmResourceRead<ImportJobResource>;
  listByParent is ArmResourceListByParent<ImportJobResource>;
}

@doc("An machine resource belonging to a site resource.")
@parentResource(ImportSiteResource)
model ImportMachineResource is ProxyResource<ImportMachineProperties> {
  @doc("Machine name")
  @key("machineName")
  @segment("machines")
  @path
  @pattern("^[a-zA-Z0-9-]{3,24}$")
  name: string;
}

@armResourceOperations
@added(Versions.v2019_05_01_preview)
interface ImportMachines {
  @tag("MachinesController")
  get is ArmResourceRead<ImportMachineResource>;

  @tag("ImportMachinesController")
  listByParent is ArmResourceListByParent<
    ImportMachineResource,
    {
      ...SubscriptionIdParameter;
      ...ResourceGroupParameter;
      ...ApiVersionParameter;

      #suppress "@azure-tools/cadl-azure-core/casing-style"
      @doc("filter query")
      @query
      $filter: string;

      @doc("page size  query")
      @query
      pagesize: string;

      @query
      @doc("Optional parameter for continuation token.")
      continuationToken: string;

      @query
      @doc("Total count of machines in the given site.")
      totalRecordCount: int32;
    }
  >;

  @tag("ImportMachinesController")
  delete is ArmResourceDeleteSync<ImportMachineResource>;
}

@armResourceOperations
interface ImportSites {
  @tag("ImportSitesController")
  @added(Versions.v2019_05_01_preview)
  get is ArmResourceRead<ImportSiteResource>;

  @added(Versions.v2019_05_01_preview)
  create is ArmResourceCreateOrUpdateSync<ImportSiteResource>;

  @added(Versions.v2019_05_01_preview)
  update is ArmResourcePatchSync<ImportSiteResource, SitesProperties>;

  @tag("ImportSitesController")
  @added(Versions.v2019_05_01_preview)
  delete is ArmResourceDeleteSync<ImportSiteResource>;

  @tag("ImportSitesController")
  @doc("Method to generate report containing \r\n            machine and the deep discovery of the application installed in the machine.")
  @added(Versions.v2019_05_01_preview)
  ExportUri is ArmResourceActionSync<
    ImportSiteResource,
    SasUriResponse,
    SasUriResponse
  >;

  @doc("Method to refresh a site.")
  @tag("SitesController")
  @added(Versions.v2019_05_01_preview)
  ImportUri is ArmResourceActionAsync<
    ImportSiteResource,
    SasUriResponse,
    SasUriResponse
  >;

  @doc("Method to get all delete import machines job for the given site.")
  @armResourceRead(InnerError)
  @segment("deletejobs")
  @get
  @added(Versions.v2020_02_01)
  deletejobs(
    ...ResourceInstanceParameters<ImportSiteResource>
  ): ArmResponse<ImportDeleteImportMachinesJobDeleteImportMachinesJobCollection>;

  @doc("Gets the delete imported machines job with the given job name.")
  @armResourceRead(InnerError)
  @get
  @added(Versions.v2020_02_01)
  deletejob(
    ...ResourceInstanceParameters<ImportSiteResource>,

    @doc("Job Arm Name.")
    @path
    @pattern("^[a-zA-Z0-9-]{3,24}$")
    @segment("DeleteJobs")
    jobName: string
  ): ArmResponse<V20200201DeleteImportMachinesJob>;

  @doc("Method to get all export import machines job for the given site.")
  @armResourceRead(InnerError)
  @segment("exportjobs")
  @get
  @added(Versions.v2020_02_01)
  exportjobs(
    ...ResourceInstanceParameters<ImportSiteResource>
  ): ArmResponse<ImportDeleteImportMachinesJobDeleteImportMachinesJobCollection>;

  @doc("Gets the export imported machines job with the given job name.")
  @armResourceRead(InnerError)
  @get
  @added(Versions.v2020_02_01)
  exportjob(
    ...ResourceInstanceParameters<ImportSiteResource>,

    @doc("Job Arm Name.")
    @path
    @pattern("^[a-zA-Z0-9-]{3,24}$")
    @segment("ExportJobs")
    jobName: string
  ): ArmResponse<V20200201ExportImportedMachinesJobExportImportedMachinesJobCollection>;

  @doc("Method to get all import machines job for the given site.")
  @armResourceRead(InnerError)
  @segment("importjobs")
  @get
  @added(Versions.v2020_02_01)
  importjobs(
    ...ResourceInstanceParameters<ImportSiteResource>
  ): ArmResponse<V20200201ImportMachinesJobImportMachinesJobCollection>;

  @doc("Gets the import job with the given job name.")
  @armResourceRead(InnerError)
  @get
  @added(Versions.v2020_02_01)
  importjob(
    ...ResourceInstanceParameters<ImportSiteResource>,

    @doc("Job Arm Name.")
    @path
    @pattern("^[a-zA-Z0-9-]{3,24}$")
    @segment("ImportJobs")
    jobName: string
  ): ArmResponse<V20200201ImportMachinesJob>;

  @doc("Patch the delete imported machines job object.")
  @armResourceUpdate(InnerError)
  @patch
  @added(Versions.v2020_02_01)
  patchDeleteMachinesJob(
    ...ResourceInstanceParameters<ImportSiteResource>,

    @doc("Job Arm Name.")
    @path
    @pattern("^[a-zA-Z0-9-]{3,24}$")
    @segment("DeleteJobs")
    jobName: string,

    @doc("Job body with updated properties.")
    @body
    body: V20200201DeleteImportMachinesJob
  ): ArmResponse<V20200201DeleteImportMachinesJob>;

  @tag("ImportSitesController")
  @doc("Deletes the imported machines for site.")
  @added(Versions.v2020_02_01)
  deleteImportedMachines is ArmResourceActionSync<
    ImportSiteResource,
    {},
    V20190501PreviewSasUriResponse
  >;
}
