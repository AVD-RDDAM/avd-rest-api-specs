import "@cadl-lang/rest";
import "./operationsCore.cadl";

//namespace Azure.Core;
using Cadl.Http;
using Cadl.Rest;
using OpenAPI;
using Autorest;
using Azure.ResourceManager.Foundations;

namespace Azure.ResourceManager;
@autoRoute
@extension("x-ms-long-running-operation", true)
@extension(
  "x-ms-long-running-operation-options",
  {
    "final-state-via":"azure-async-operation",
  }
)
@armResourceAction(TResource)
@post
@doc("Operation template for creating async arm resource action without body.")
op ArmResourceActionAsyncNoRequestBody<
  TResource extends ArmResource,
  TResponse extends object,
  TBaseParameters = BaseParameters<TResource>
>(
  ...ResourceInstanceParameters<TResource, TBaseParameters>,

):   ArmAcceptedResponse ;


@autoRoute
@extension("x-ms-long-running-operation", true)
@extension(
  "x-ms-long-running-operation-options",
  {
    "final-state-via": "azure-async-operation",
  }
)
@armResourceAction(TResource)
@post
op ArmResourceActionNoContentNoBodyAsync<
  TResource extends ArmResource,
  TBaseParameters = BaseParameters<TResource>
>(
  ...ResourceInstanceParameters<TResource, TBaseParameters>,

): ArmNoContentResponse<"Action completed successfully."> | ArmAcceptedResponse ;


@doc("The response of a {name} list operation.", TResource)
@friendlyName("{name}Collection", TResource)
model ResourceListResultUpdated<TResource extends ArmResource> is Azure.Core.PageUpdated<TResource>;

@get
@autoRoute
@list(TResource)
@segmentOf(TResource)
@armRenameListByOperation(TResource, TParentName, TParentFriendlyName) // This must come before @armResourceList!
@armResourceList(TResource)
op ArmResourceListByParentUpdated<
  TResource extends ArmResource,
  TBaseParameters = BaseParameters<TResource>,
  TParentName = "",
  TParentFriendlyName = ""
>(
  ...ResourceParentParameters<TResource, TBaseParameters>
): ArmResponse<ResourceListResultUpdated<TResource>> ;


@autoRoute
@extension("x-ms-long-running-operation", true)
@extension(
  "x-ms-long-running-operation-options",
  {
    "final-state-via": "azure-async-operation",
  }
)
@armResourceAction(TResource)
@post
op ArmResourceActionNoContentAsyncCustomMapRequest<
  TResource extends ArmResource,
  TRequest extends object,
  TBaseParameters = BaseParameters<TResource>
>(
  ...ResourceInstanceParameters<TResource, TBaseParameters>,

  @doc("The content of the action request")
  @body
  mapRequest: TRequest
): ArmNoContentResponse<"Action completed successfully."> | ArmAcceptedResponse ;


@autoRoute
@doc("Create a {name}", TResource)
@extension("x-ms-long-running-operation", true)
@extension(
  "x-ms-long-running-operation-options",
  {
    "final-state-via": "azure-async-operation",
  }
)
@armResourceCreateOrUpdate(TResource)
@put
op ArmResourceCreateOrUpdateAsyncCustom<
  TResource extends ArmResource,
  TBaseParameters = BaseParameters<TResource>
>(
  ...ResourceInstanceParameters<TResource, TBaseParameters>,

  @doc("Resource create parameters.")
  @body
  body: TResource
):  ArmAcceptedResponse<"Accepted."> ;

@autoRoute
@doc("Create a {name}", TResource)
@armResourceCreateOrUpdate(TResource)
@put
op ArmResourceCreateOrUpdateSyncCustom<
  TResource extends ArmResource,
  TBaseParameters = BaseParameters<TResource>
>(
  ...ResourceInstanceParameters<TResource, TBaseParameters>,

  @doc("Resource create parameters.")
  @body
  body: TResource
): ArmResponse<TResource> ;

@autoRoute
@armResourceAction(TResource)
@post
op ArmResourceActionSyncNoRequestBody<
  TResource extends ArmResource,
  TResponse extends object,
  TBaseParameters = BaseParameters<TResource>
>(
  ...ResourceInstanceParameters<TResource, TBaseParameters>,
): ArmResponse<TResponse> ;

/**
 * This is the interface that implements the standard ARM operation that returns
 * all supported RP operations. You should have exactly one declaration for each
 * ARM service. It implements
 *   GET "/providers/Microsoft.ContosoProviderHub/operations"
 */
interface OperationsCustom {
  @tag("Operations")
  @autoRoute
  @armUpdateProviderNamespace
  @doc("List the operations for the provider")
  @segment("operations")
  @get
  list(
    ...ApiVersionParameter,

    @path
    @segment("providers")
    @doc("The provider namespace (this parameter will not show up in operations).")
    provider: "Microsoft.ThisWillBeReplaced"
  ): ArmResponse<OperationListResult> ;
}

